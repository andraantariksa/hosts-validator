// https://www.ietf.org/rfc/rfc3986.txt

whitespace = _{ " " | "\t" }

ipv4_chunk = { ('1'..'2') ~ ASCII_DIGIT{2} | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT | ASCII_DIGIT }
ipv4 = { ipv4_chunk ~ "." ~ ipv4_chunk ~ "." ~ ipv4_chunk ~ "." ~ ipv4_chunk }

ipv6_chunk = { ASCII_HEX_DIGIT{1,4} }
ipv6_least32 = { (ipv6_chunk ~ ":" ~ ipv6_chunk) | ipv4 }
ipv6 = {
      (                                                                       (ipv6_chunk ~ ":"){6} ~ ipv6_least32)
    | (                                                                "::" ~ (ipv6_chunk ~ ":"){5} ~ ipv6_least32)
    | ((ipv6_chunk)?                                                 ~ "::" ~ (ipv6_chunk ~ ":"){4} ~ ipv6_least32)
    | (((!(ipv6_chunk ~ "::") ~ ipv6_chunk ~ ":"){,1} ~ ipv6_chunk)? ~ "::" ~ (ipv6_chunk ~ ":"){3} ~ ipv6_least32)
    | (((!(ipv6_chunk ~ "::") ~ ipv6_chunk ~ ":"){,2} ~ ipv6_chunk)? ~ "::" ~ (ipv6_chunk ~ ":"){2} ~ ipv6_least32)
    | (((!(ipv6_chunk ~ "::") ~ ipv6_chunk ~ ":"){,3} ~ ipv6_chunk)? ~ "::" ~ (ipv6_chunk ~ ":")    ~ ipv6_least32)
    | (((!(ipv6_chunk ~ "::") ~ ipv6_chunk ~ ":"){,4} ~ ipv6_chunk)? ~ "::" ~                         ipv6_least32)
    | (((!(ipv6_chunk ~ "::") ~ ipv6_chunk ~ ":"){,5} ~ ipv6_chunk)? ~ "::" ~ ipv6_chunk                          )
    | (((!(ipv6_chunk ~ "::") ~ ipv6_chunk ~ ":"){,6} ~ ipv6_chunk)? ~ "::"                                       )
}

ip = @{ ipv4 | ipv6 }

hostname = @{ (ASCII_ALPHANUMERIC | "-" | ".")+ }
hostnames = { (whitespace+ ~ hostname)* ~ whitespace* }

comment = _{ "#" ~ (!"\n" ~ ANY)* }
statement = { ip ~ hostnames }
main = _{ SOI ~ "\n"* ~ (statement? ~ comment? ~ ("\r"? ~ "\n")+)* ~ statement? ~ EOI }